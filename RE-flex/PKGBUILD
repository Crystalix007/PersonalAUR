_pkgname="RE-flex"
pkgname="${_pkgname}-git"
pkgver=v1.3.8.r0.gd3820779
pkgrel=1
pkgdesc="The regex-centric, fast and flexible scanner generator for C++"
arch=('x86_64')
url="https://github.com/Genivia/RE-flex"
license=('BSD')
depends=()
provides=('RE-flex')
conflicts=()
optdepends=('graphviz: To visualize the FSM graphs generated with reflex option '"'"'--graphs-file'"'"
			'boost: To use Boost.Regex as a regex engine with the RE/flex library and scanner generator')
epoch=1
source=(git+https://github.com/Genivia/RE-flex.git)
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}"
	autoreconf -fi
	./configure --mandir=/usr/share/man/ --prefix=/usr/
}

build() {
	cd "${_pkgname}"
	make
}

check() {
	cd "${_pkgname}"
	reflex --version >/dev/null
}

package() {
	cd "${_pkgname}"
	make DESTDIR="${pkgdir}/" install
	install -m644 -D LICENSE.txt "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
	install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README"
}
