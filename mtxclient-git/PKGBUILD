pkgname=mtxclient-git
pkgver=v0.2.0.r32.g6f7b35a
pkgrel=1
pkgdesc="Client API library for Matrix, built on top of Boost.Asio"
arch=('x86_64' 'i686')
url="https://github.com/Nheko-Reborn/mtxclient"
license=('MIT')
depends=('libolm' 'libsodium' 'openssl' 'boost-libs')
makedepends=('cmake' 'nlohmann-json' 'boost')
source=("$pkgname::git+https://github.com/Nheko-Reborn/mtxclient.git"
        "CMakeLists.patch")
sha256sums=('SKIP'
            '4a45205b07ece8e24db6fb5c513cd331a058b1018a3f32cd35ab93fe40a83c02')
conflicts=('mtxclient')
provides=('mtxclient')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd "$pkgname"
  pwd
  git apply -3 ../CMakeLists.patch
  cd ../build
  cmake "../$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_LIB_TESTS=OFF \
    -DBUILD_LIB_EXAMPLES=OFF \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "../$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}





